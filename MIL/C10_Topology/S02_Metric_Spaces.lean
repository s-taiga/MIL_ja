import MIL.Common
import Mathlib.Topology.Instances.Real
import Mathlib.Analysis.Normed.Operator.BanachSteinhaus

open Set Filter
open Topology Filter

/- OMIT:
Metric spaces
--------------

OMIT. -/
/- TEXT:
.. index:: metric space

.. _metric_spaces:

è·é›¢ç©ºé–“
---------

TEXT. -/
/- OMIT:
Examples in the previous section focus on sequences of real numbers. In this section we will go up a bit in generality and focus on
metric spaces. A metric space is a type ``X`` equipped with a distance function ``dist : X â†’ X â†’ â„`` which is a generalization of
the function ``fun x y â†¦ |x - y|`` from the case where ``X = â„``.

OMIT. -/
/- TEXT:
å‰ç¯€ã®ä¾‹é¡Œã¯å®Ÿæ•°ã®æ•°åˆ—ã«ç„¦ç‚¹ã‚’ã‚ã¦ã¾ã—ãŸï¼ã“ã®ã‚»ã‚¯ã‚·ãƒ§ãƒ³ã§ã¯ä¸€èˆ¬æ€§ã‚’å°‘ã—ä¸Šã’ã¦è·é›¢ç©ºé–“(metric space)ã«ç„¦ç‚¹ã‚’ã‚ã¦ã¾ã™ï¼è·é›¢ç©ºé–“ã¨ã¯è·é›¢é–¢æ•° ``dist : X â†’ X â†’ â„`` ã‚’æŒã¤ ``X`` å‹ã®ã“ã¨ã§ï¼Œ ``fun x y â†¦ |x - y|`` ã‚’ ``X = â„`` ã®å ´åˆã‹ã‚‰ä¸€èˆ¬åŒ–ã—ãŸã‚‚ã®ã§ã™ï¼

TEXT. -/
/- OMIT:
Introducing such a space is easy and we will check all properties required from the distance function.
OMIT. -/
/- TEXT:
ã“ã®ã‚ˆã†ãªç©ºé–“ã®å°å…¥ã¯ç°¡å˜ã§ï¼Œè·é›¢é–¢æ•°ã‹ã‚‰å¿…è¦ãªç‰¹æ€§ã‚’ã™ã¹ã¦ç¢ºèªã§ãã¾ã™ï¼
BOTH: -/
-- QUOTE:
variable {X : Type*} [MetricSpace X] (a b c : X)

#check (dist a b : â„)
#check (dist_nonneg : 0 â‰¤ dist a b)
#check (dist_eq_zero : dist a b = 0 â†” a = b)
#check (dist_comm a b : dist a b = dist b a)
#check (dist_triangle a b c : dist a c â‰¤ dist a b + dist b c)
-- QUOTE.

/- OMIT:
Note we also have variants where the distance can be infinite or where ``dist a b`` can be zero without having ``a = b`` or both.
They are called ``EMetricSpace``, ``PseudoMetricSpace`` and ``PseudoEMetricSpace`` respectively (here "e" stands for "extended").

OMIT. -/
/- TEXT:
ã¾ãŸï¼Œè·é›¢ãŒç„¡é™å¤§ã«ãªã‚‹ã‚‚ã®ã‚„ï¼Œ ``a = b`` ã§ãªãã¨ã‚‚ ``dist a b`` ãŒ0ã«ãªã‚‹ã‚‚ã®ï¼Œã‚ã‚‹ã„ã¯ãã®ä¸¡æ–¹ãŒå­˜åœ¨ã™ã‚‹ã‚‚ã®ã‚‚ã‚ã‚Šã¾ã™ï¼ã“ã‚Œã‚‰ã¯ãã‚Œãã‚Œ ``EMetricSpace`` ï¼Œ ``PseudoMetricSpace`` ï¼Œ ``PseudoEMetricSpace`` ã¨å‘¼ã°ã‚Œã¾ã™ï¼ˆã“ã“ã§"e"ã¯"extends"ã‚’è¡¨ã—ã¾ã™ï¼‰ï¼
BOTH: -/
-- Note the next three lines are not quoted, their purpose is to make sure those things don't get renamed while we're looking elsewhere.
#check EMetricSpace
#check PseudoMetricSpace
#check PseudoEMetricSpace

/- OMIT:
Note that our journey from ``â„`` to metric spaces jumped over the special case of normed spaces that also require linear algebra and
will be explained as part of the calculus chapter.

OMIT. -/
/- TEXT:
``â„`` ã‹ã‚‰è·é›¢ç©ºé–“ã¸ã®æ—…ã¯ç·šå½¢ä»£æ•°ã‚’å¿…è¦ã¨ã™ã‚‹ãƒãƒ«ãƒ ç©ºé–“ã®ç‰¹æ®Šãªã‚±ãƒ¼ã‚¹ã‚’é£›ã³è¶ŠãˆãŸã“ã¨ã«æ³¨æ„ã—ã¦ãã ã•ã„ï¼ãƒãƒ«ãƒ ç©ºé–“ã«ã¤ã„ã¦ã¯å¾®ç©åˆ†ã®ç« ã§èª¬æ˜ã—ã¾ã™ï¼

TEXT. -/
/- OMIT:
Convergence and continuity
^^^^^^^^^^^^^^^^^^^^^^^^^^

OMIT. -/
/- TEXT:
åæŸã¨é€£ç¶šæ€§
^^^^^^^^^^^^^

TEXT. -/
/- OMIT:
Using distance functions, we can already define convergent sequences and continuous functions between metric spaces.
They are actually defined in a more general setting covered in the next section,
but we have lemmas recasting the definition in terms of distances.
OMIT. -/
/- TEXT:
è·é›¢é–¢æ•°ã‚’ä½¿ãˆã°ï¼ŒåæŸåˆ—ã¨è·é›¢ç©ºé–“ã®ã‚ã„ã ã®é€£ç¶šé–¢æ•°ã‚’å®šç¾©ã§ãã¾ã™ï¼ã“ã‚Œã‚‰ã®é–¢æ•°ã¯å®Ÿéš›ã«ã¯æ¬¡ç¯€ã§èª¬æ˜ã™ã‚‹ï¼Œã‚ˆã‚Šä¸€èˆ¬çš„ãªè¨­å®šã§å®šç¾©ã•ã‚Œã¦ã„ã¾ã™ï¼ã—ã‹ã—ï¼Œã“ã®å®šç¾©ã‚’è·é›¢ã®ç”¨èªã«ç½®ãæ›ãˆãŸè£œé¡ŒãŒã‚ã‚Šã¾ã™ï¼
BOTH: -/
-- QUOTE:
example {u : â„• â†’ X} {a : X} :
    Tendsto u atTop (ğ“ a) â†” âˆ€ Îµ > 0, âˆƒ N, âˆ€ n â‰¥ N, dist (u n) a < Îµ :=
  Metric.tendsto_atTop

example {X Y : Type*} [MetricSpace X] [MetricSpace Y] {f : X â†’ Y} :
    Continuous f â†”
      âˆ€ x : X, âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ x', dist x' x < Î´ â†’ dist (f x') (f x) < Îµ :=
  Metric.continuous_iff
-- QUOTE.

/- OMIT:
A *lot* of lemmas have some continuity assumptions, so we end up proving a lot of continuity results and there
is a ``continuity`` tactic devoted to this task. Let's prove a continuity statement that will be needed
in an exercise below. Notice that Lean knows how to treat a product of two metric spaces as a metric space, so
it makes sense to consider continuous functions from ``X Ã— X`` to ``â„``.
In particular the (uncurried version of the) distance function is such a function.

OMIT. -/
/- TEXT:
.. index:: continuity, tactics ; continuity

*å¤šã* ã®è£œé¡ŒãŒé€£ç¶šæ€§ã‚’ä»®å®šã—ã¦ã„ã‚‹ãŸã‚ï¼ŒãŸãã•ã‚“ã®é€£ç¶šæ€§ã®çµæœã‚’è¨¼æ˜ã™ã‚‹ã¯ã‚ã«ãªã‚Šã¾ã™ï¼ã“ã®ã‚¿ã‚¹ã‚¯ã®ãŸã‚ã« ``continuity`` ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ãŒç”¨æ„ã•ã‚Œã¦ã„ã¾ã™ï¼ä»¥ä¸‹ã®æ¼”ç¿’å•é¡Œã§å¿…è¦ã¨æˆã‚‹é€£ç¶šæ€§ã‚’è¨¼æ˜ã—ã¦ã¿ã¾ã—ã‚‡ã†ï¼Leanã¯2ã¤ã®è·é›¢ç©ºé–“ã®ç©ã‚’è·é›¢ç©ºé–“ã¨ã—ã¦æ‰±ã†æ–¹æ³•ã‚’çŸ¥ã£ã¦ã„ã‚‹ãŸã‚ï¼Œ ``X Ã— X`` ã‹ã‚‰ ``â„`` ã¸ã®é€£ç¶šé–¢æ•°ã‚’è€ƒãˆã‚‹ã“ã¨ã¯ç†ã«ã‹ãªã£ã¦ã„ã¾ã™ï¼ç‰¹ã«ï¼ˆéã‚«ãƒªãƒ¼åŒ–ã•ã‚ŒãŸï¼‰è·é›¢é–¢æ•°ãŒãã®ã‚ˆã†ãªé–¢æ•°ã§ã™ï¼

BOTH: -/
-- QUOTE:
example {X Y : Type*} [MetricSpace X] [MetricSpace Y] {f : X â†’ Y} (hf : Continuous f) :
    Continuous fun p : X Ã— X â†¦ dist (f p.1) (f p.2) := by continuity
-- QUOTE.

/- OMIT:
This tactic is a bit slow, so it is also useful to know
how to do it by hand. We first need to use that ``fun p : X Ã— X â†¦ f p.1`` is continuous because it
is the composition of ``f``, which is continuous by assumption ``hf``, and the projection ``prod.fst`` whose continuity
is the content of the lemma ``continuous_fst``. The composition property is ``Continuous.comp`` which is
in the ``Continuous`` namespace so we can use dot notation to compress
``Continuous.comp hf continuous_fst`` into ``hf.comp continuous_fst`` which is actually more readable
since it really reads as composing our assumption and our lemma.
We can do the same for the second component to get continuity of ``fun p : X Ã— X â†¦ f p.2``. We then assemble
those two continuities using ``Continuous.prod_mk`` to get
``(hf.comp continuous_fst).prod_mk (hf.comp continuous_snd) : Continuous (fun p : X Ã— X â†¦ (f p.1, f p.2))``
and compose once more to get our full proof.
OMIT. -/
/- TEXT:
ã“ã®ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ã¯å°‘ã—æŒ™å‹•ãŒé…ã„ãŸã‚ï¼Œã“ã‚Œã¨åŒç­‰ã®å†…å®¹ã‚’æ‰‹ä½œæ¥­ã§è¡Œã†æ–¹æ³•ã‚’çŸ¥ã£ã¦ãŠãã¨è‰¯ã„ã§ã—ã‚‡ã†ï¼ã¾ãš ``fun p : X Ã— X â†¦ f p.1`` ãŒé€£ç¶šã§ã‚ã‚‹ã¨ã„ã†äº‹å®Ÿã‚’ä½¿ã†å¿…è¦ãŒã‚ã‚Šã¾ã™ï¼ã“ã®äº‹å®Ÿã¯ä»®å®š ``hf`` ã«ã‚ˆã£ã¦é€£ç¶šã§ã‚ã‚‹ ``f`` ã¨è£œé¡Œ ``continuous_fst`` ã®å†…å®¹ã‹ã‚‰é€£ç¶šæ€§ã‚’æŒã¤ ``prod.fst`` ã¨ã®åˆæˆã‹ã‚‰æˆã‚Šç«‹ã¡ã¾ã™ï¼ã“ã®åˆæˆã®æ€§è³ªã¯ ``Continuous.comp`` ã«ã‚ˆã‚‹ã‚‚ã®ã§ã™ï¼ã“ã‚Œã¯ ``Continuous`` åå‰ç©ºé–“ã«ã‚ã‚‹ãŸã‚ï¼Œ ``Continuous.comp hf continuous_fst`` ã¨ã„ã†æ›¸ãæ–¹ã¯ãƒ‰ãƒƒãƒˆã«ã‚ˆã‚‹è¡¨è¨˜æ³•ã§ ``hf.comp continuous_fst`` ã«çŸ­ç¸®ã™ã‚‹ã“ã¨ãŒã§ãã¾ã™ï¼ã“ã†ã™ã‚‹ã“ã¨ã§ã¾ã•ã«ä»®å®šã¨è£œé¡Œã‚’åˆæˆã—ã¦ã„ã‚‹ã¨ã„ã†ã‚ˆã†ã«èª­ã‚ã‚‹ãŸã‚ï¼Œã‚ˆã‚Šå¯èª­æ€§ãŒä¸ŠãŒã‚Šã¾ã™ï¼2ã¤ç›®ã®æ§‹æˆè¦ç´ ã«ã¤ã„ã¦ã‚‚åŒã˜ã‚ˆã†ã«ã—ã¦ ``fun p : X Ã— X â†¦ f p.2`` ã®é€£ç¶šæ€§ãŒå¾—ã‚‰ã‚Œã¾ã™ï¼æ¬¡ã«ã“ã®2ã¤ã®é€£ç¶šæ€§ã‚’ ``Continuous.prod_mk`` ã‚’ä½¿ã£ã¦ ``(hf.comp continuous_fst).prod_mk (hf.comp continuous_snd) : Continuous (fun p : X Ã— X â†¦ (f p.1, f p.2))`` ã¨ãªã‚Šï¼Œã¾ãŸã‚‚ã†ä¸€å›åˆæˆã™ã‚‹ã“ã¨ã§å®Œå…¨ãªè¨¼æ˜ã«ãªã‚Šã¾ã™ï¼
BOTH: -/
-- QUOTE:
example {X Y : Type*} [MetricSpace X] [MetricSpace Y] {f : X â†’ Y} (hf : Continuous f) :
    Continuous fun p : X Ã— X â†¦ dist (f p.1) (f p.2) :=
  continuous_dist.comp ((hf.comp continuous_fst).prod_mk (hf.comp continuous_snd))
-- QUOTE.

/- OMIT:
The combination of ``Continuous.prod_mk`` and ``continuous_dist`` via ``Continuous.comp`` feels clunky,
even when heavily using dot notation as above. A more serious issue is that this nice proof requires a lot of
planning. Lean accepts the above proof term because it is a full term proving a statement which is
definitionally equivalent to our goal, the crucial definition to unfold being that of a composition of functions.
Indeed our target function ``fun p : X Ã— X â†¦ dist (f p.1) (f p.2)`` is not presented as a composition.
The proof term we provided proves continuity of ``dist âˆ˜ (fun p : X Ã— X â†¦ (f p.1, f p.2))`` which happens
to be definitionally equal to our target function. But if we try to build this proof gradually using
tactics starting with ``apply continuous_dist.comp`` then Lean's elaborator will fail to recognize a
composition and refuse to apply this lemma. It is especially bad at this when products of types are involved.

OMIT. -/
/- TEXT:
``Continuous.prod_mk`` ã¨ ``Continuous.comp`` ã‚’ä»‹ã—ãŸ ``continuous_dist`` ã®çµ„ã¿åˆã‚ã›ã¯ï¼Œä»®ã«ä¸Šè¨˜ã®ã‚ˆã†ãªãƒ‰ãƒƒãƒˆè¡¨è¨˜ã‚’å¤šç”¨ã—ãŸã¨ã—ã¦ã‚‚ä¸æ ¼å¥½ã«ãªã£ã¦ã—ã¾ã„ã¾ã™ï¼ã—ã‹ã—ã‚ˆã‚Šé‡è¦ãªå•é¡Œã¯ã“ã®ç¶ºéº—ãªè¨¼æ˜ã‚’æ§‹ç¯‰ã™ã‚‹ã«ã¯äº‹å‰ã«å¤šãã®è¨ˆç”»ã‚’ã™ã‚‹å¿…è¦ãŒã‚ã‚‹ã¨ã„ã†ã“ã¨ã§ã™ï¼LeanãŒä¸Šè¨˜ã®è¨¼æ˜é …ã‚’å—ã‘å…¥ã‚Œã‚‹ã®ã¯ï¼Œãã‚ŒãŒã‚´ãƒ¼ãƒ«ã¨å®šç¾©çš„ã«ç­‰ä¾¡ãªæ–‡ã‚’è¨¼æ˜ã™ã‚‹å®Œå…¨ãªé …ã ã‹ã‚‰ã§ã™ï¼å®Ÿéš›ï¼Œã‚´ãƒ¼ãƒ«ã®é–¢æ•° ``fun p : X Ã— X â†¦ dist (f p.1) (f p.2)`` ã¯åˆæˆã¨ã—ã¦æç¤ºã•ã‚Œã¦ã„ã¾ã›ã‚“ï¼ä¸Šè¨˜ã®è¨¼æ˜é …ã¯ ``dist âˆ˜ (fun p : X Ã— X â†¦ (f p.1, f p.2))`` ã®é€£ç¶šæ€§ã‚’ç›®æ¨™ã®é–¢æ•°ã¨ãŸã¾ãŸã¾å®šç¾©çš„ã«ç­‰ã—ã„ã“ã¨ã‹ã‚‰è¨¼æ˜ã—ã¦ã„ã¾ã™ï¼ã—ã‹ã—ï¼Œã“ã®è¨¼æ˜ã‚’ ``apply continuous_dist.comp`` ã‹ã‚‰å§‹ã¾ã‚‹ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ã‚¹ã‚¿ã‚¤ãƒ«ã§é€æ¬¡çš„ã«æ§‹ç¯‰ã—ã‚ˆã†ã¨ã™ã‚‹ã¨ï¼ŒLeanã®ã‚¨ãƒ©ãƒœãƒ¬ãƒ¼ã‚¿ã¯åˆæˆã‚’èªè­˜ã§ããšï¼Œã“ã®è£œé¡Œã®é©ç”¨ã‚’æ‹’å¦ã—ã¾ã™ï¼ç‰¹ã«å‹ã®ç©ãŒé–¢ä¿‚ã™ã‚‹å ´åˆã‚’è‹¦æ‰‹ã¨ã—ã¦ã„ã¾ã™ï¼

TEXT. -/
/- OMIT:
A better lemma to apply here is
``Continuous.dist {f g : X â†’ Y} : Continuous f â†’ Continuous g â†’ Continuous (fun x â†¦ dist (f x) (g x))``
which is nicer to Lean's elaborator and also provides a shorter proof when directly providing a full
proof term, as can be seen from the following two new proofs of the above statement:
OMIT. -/
/- TEXT:
ã“ã“ã§ã¯ã‚€ã—ã‚è£œé¡Œ ``Continuous.dist {f g : X â†’ Y} : Continuous f â†’ Continuous g â†’ Continuous (fun x â†¦ dist (f x) (g x))`` ã‚’é©ç”¨ã™ã‚‹ã®ãŒã‚ˆã„ã§ã—ã‚‡ã†ï¼ã“ã‚Œã¯Leanã®ã‚¨ãƒ©ãƒœãƒ¬ãƒ¼ã‚¿ã«ã¨ã£ã¦ã‚ˆã‚Šè‰¯ã„ã‚‚ã®ã§ã‚ã‚Šï¼Œã¾ãŸå…ˆç¨‹ã®æ–‡ã«å¯¾ã™ã‚‹ä»¥ä¸‹ã®2ã¤ã®æ–°ã—ã„è¨¼æ˜ã‹ã‚‰ã‚ã‹ã‚‹ã‚ˆã†ã«ï¼Œå®Œå…¨ãªè¨¼æ˜é …ãŒã‚ˆã‚ŠçŸ­ããªã‚Šã¾ã™ï¼š
BOTH: -/
-- QUOTE:
example {X Y : Type*} [MetricSpace X] [MetricSpace Y] {f : X â†’ Y} (hf : Continuous f) :
    Continuous fun p : X Ã— X â†¦ dist (f p.1) (f p.2) := by
  apply Continuous.dist
  exact hf.comp continuous_fst
  exact hf.comp continuous_snd

example {X Y : Type*} [MetricSpace X] [MetricSpace Y] {f : X â†’ Y} (hf : Continuous f) :
    Continuous fun p : X Ã— X â†¦ dist (f p.1) (f p.2) :=
  (hf.comp continuous_fst).dist (hf.comp continuous_snd)
-- QUOTE.

/- OMIT:
Note that, without the elaboration issue coming from composition, another way to compress
our proof would be to use ``Continuous.prod_map`` which is sometimes useful and gives
as an alternate proof term ``continuous_dist.comp (hf.prod_map hf)`` which even shorter to type.

OMIT. -/
/- TEXT:
åˆæˆã‹ã‚‰ç”Ÿã˜ã‚‹ã‚¨ãƒ©ãƒœãƒ¬ãƒ¼ã‚¿ã®å•é¡Œã‚’é™¤ã‘ã°ï¼Œè¨¼æ˜ã‚’åœ§ç¸®ã™ã‚‹åˆ¥ã®æ–¹æ³•ã¯ ``Continuous.prod_map`` ã‚’ä½¿ã†ã“ã¨ã§ã™ï¼ã“ã‚Œã¯æ™‚ã«ä¾¿åˆ©ã§ï¼Œã“ã‚Œã«ã‚ˆã£ã¦ã‚ˆã‚ŠçŸ­ã„è¨¼æ˜é … ``continuous_dist.comp (hf.prod_map hf)`` ã‚’ä¸ãˆã¾ã™ï¼

TEXT. -/
/- OMIT:
Since it is sad to decide between a version which is better for elaboration and a version which is shorter
to type, let us wrap this discussion with a last bit of compression offered
by ``Continuous.fst'`` which allows to compress ``hf.comp continuous_fst`` to ``hf.fst'`` (and the same with ``snd``)
and get our final proof, now bordering obfuscation.

OMIT. -/
/- TEXT:
ã‚¨ãƒ©ãƒœãƒ¬ãƒ¼ã‚¿ã«éƒ½åˆã®è‰¯ã„ãƒãƒ¼ã‚¸ãƒ§ãƒ³ã¨å‹ã«å¯¾ã—ã¦çŸ­ããªã‚‹ãƒãƒ¼ã‚¸ãƒ§ãƒ³ã®é–“ã‚’æ±ºã‚ã‚‹ã®ã¯ãƒ„ãƒ©ã‚¤ãŸã‚ï¼Œã“ã®è­°è«–ã¯ ``Continuous.fst'`` ãŒæä¾›ã™ã‚‹åœ§ç¸®ã®æœ€å¾Œã®ã²ã¨æ‰‹é–“ã‚’åŠ ãˆã‚‹ã“ã¨ã§ï¼Œ ``hf.comp continuous_fst`` ã‚’ ``hf.fst'`` ã«ï¼ˆ ``snd`` ã‚‚åŒæ§˜ï¼‰åœ§ç¸®ã™ã‚‹ã“ã¨ãŒã§ãï¼Œé›£èª­åŒ–ã¨ã®å¢ƒç•Œã‚’è¶…ãˆãŸæœ€çµ‚çš„ãªè¨¼æ˜ã‚’å¾—ã‚‹ã“ã¨ãŒã§ãã¾ã™ï¼
BOTH: -/
-- QUOTE:
example {X Y : Type*} [MetricSpace X] [MetricSpace Y] {f : X â†’ Y} (hf : Continuous f) :
    Continuous fun p : X Ã— X â†¦ dist (f p.1) (f p.2) :=
  hf.fst'.dist hf.snd'
-- QUOTE.

/- OMIT:
It's your turn now to prove some continuity lemma. After trying the continuity tactic, you will need
``Continuous.add``, ``continuous_pow`` and ``continuous_id`` to do it by hand.

OMIT. -/
/- TEXT:
ä»Šåº¦ã¯èª­è€…ãŒé€£ç¶šæ€§ã®è£œé¡Œã‚’è¨¼æ˜ã™ã‚‹ç•ªã§ã™ï¼continuityã‚¿ã‚¯ãƒ†ã‚£ã‚¯ã‚’ä½¿ã£ãŸå¾Œã«ï¼Œæ‰‹ä½œæ¥­ã§ ``Continuous.add`` ï¼Œ  ``continuous_pow`` ï¼Œ ``continuous_id`` ã‚’ä½¿ã†å¿…è¦ãŒã‚ã‚Šã¾ã™ï¼
BOTH: -/
-- QUOTE:
example {f : â„ â†’ X} (hf : Continuous f) : Continuous fun x : â„ â†¦ f (x ^ 2 + x) :=
  sorry
-- QUOTE.

-- SOLUTIONS:
example {f : â„ â†’ X} (hf : Continuous f) : Continuous fun x : â„ â†¦ f (x ^ 2 + x) :=
  hf.comp <| (continuous_pow 2).add continuous_id

/- OMIT:
So far we saw continuity as a global notion, but one can also define continuity at a point.
OMIT. -/
/- TEXT:
ã“ã“ã¾ã§ã¯ã‚°ãƒ­ãƒ¼ãƒãƒ«ãªæ¦‚å¿µã¨ã—ã¦ã®é€£ç¶šæ€§ã‚’è¦‹ã¦ãã¾ã—ãŸãŒï¼Œã‚ã‚‹ç‚¹ã§ã®é€£ç¶šæ€§ã‚’å®šç¾©ã™ã‚‹ã“ã¨ã‚‚ã§ãã¾ã™ï¼
BOTH: -/
-- QUOTE:
example {X Y : Type*} [MetricSpace X] [MetricSpace Y] (f : X â†’ Y) (a : X) :
    ContinuousAt f a â†” âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ {x}, dist x a < Î´ â†’ dist (f x) (f a) < Îµ :=
  Metric.continuousAt_iff
-- QUOTE.

/- OMIT:

Balls, open sets and closed sets
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

OMIT. -/
/- TEXT:

çƒä½“ï¼Œé–‹é›†åˆï¼Œé–‰é›†åˆ
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

TEXT. -/
/- OMIT:
Once we have a distance function, the most important geometric definitions are (open) balls and closed balls.

OMIT. -/
/- TEXT:
è·é›¢é–¢æ•°ã‚’å¾—ãŸã®ã§ï¼Œæ¬¡ã¯æœ€ã‚‚é‡è¦ãªå¹¾ä½•å­¦çš„å®šç¾©ã§ã‚ã‚‹ï¼ˆé–‹ï¼‰çƒä½“ã¨é–‰çƒä½“ã§ã™ï¼
BOTH: -/
-- QUOTE:
variable (r : â„)

example : Metric.ball a r = { b | dist b a < r } :=
  rfl

example : Metric.closedBall a r = { b | dist b a â‰¤ r } :=
  rfl
-- QUOTE.

/- OMIT:
Note that `r` is any real number here, there is no sign restriction. Of course some statements do require a radius condition.
OMIT. -/
/- TEXT:
ã“ã“ã§ `r` ã¯ä»»æ„ã®å®Ÿæ•°ã§ã‚ã‚Šï¼Œç¬¦å·ã®åˆ¶é™ãŒç„¡ã„ã“ã¨ã«æ³¨ç›®ã—ã¦ãã ã•ã„ï¼ã‚‚ã¡ã‚ã‚“ï¼ŒåŠå¾„ã®æ¡ä»¶ãŒå¿…è¦ãªæ–‡ã‚‚å­˜åœ¨ã—ã¾ã™ï¼
BOTH: -/
-- QUOTE:
example (hr : 0 < r) : a âˆˆ Metric.ball a r :=
  Metric.mem_ball_self hr

example (hr : 0 â‰¤ r) : a âˆˆ Metric.closedBall a r :=
  Metric.mem_closedBall_self hr
-- QUOTE.

/- OMIT:
Once we have balls, we can define open sets. They are actually defined in a more general setting covered in the next section,
but we have lemmas recasting the definition in terms of balls.

OMIT. -/
/- TEXT:
çƒä½“ãŒã‚ã‚Œã°ï¼Œé–‹é›†åˆã‚’å®šç¾©ã™ã‚‹ã“ã¨ãŒã§ãã¾ã™ï¼å®Ÿéš›ã«ã¯ï¼Œé–‹é›†åˆã¯æ¬¡ã®ç¯€ã§èª¬æ˜ã™ã‚‹ã‚ˆã‚Šä¸€èˆ¬çš„ãªè¨­å®šã§å®šç¾©ã•ã‚Œã¾ã™ãŒï¼Œã“ã“ã§ã¯çƒä½“ã«ã‚ˆã‚‹å®šç¾©ã«å¤‰æ›´ã—ãŸè£œé¡Œã‚’ä½¿ã„ã¾ã™ï¼
BOTH: -/
-- QUOTE:
example (s : Set X) : IsOpen s â†” âˆ€ x âˆˆ s, âˆƒ Îµ > 0, Metric.ball x Îµ âŠ† s :=
  Metric.isOpen_iff
-- QUOTE.

/- OMIT:
Then closed sets are sets whose complement is open. Their important property is they are closed under limits. The closure of a set is the smallest closed set containing it.
OMIT. -/
/- TEXT:
ã¾ãŸé–‰é›†åˆã¨ã¯ï¼Œè£œé›†åˆãŒé–‹ã§ã‚ã‚‹é›†åˆã®ã“ã¨ã§ã™ï¼ã“ã®é›†åˆã«ã¤ã„ã¦ã®é‡è¦ãªæ€§è³ªã¯ï¼Œæ¥µé™ã®ã‚‚ã¨ã§é–‰ã˜ã¦ã„ã‚‹ã“ã¨ã§ã™ï¼ã‚ã‚‹é›†åˆã®é–‰åŒ…ã¯ï¼Œãã®é›†åˆã‚’å«ã‚€æœ€å°ã®é–‰é›†åˆã§ã™ï¼
BOTH: -/
-- QUOTE:
example {s : Set X} : IsClosed s â†” IsOpen (sá¶œ) :=
  isOpen_compl_iff.symm

example {s : Set X} (hs : IsClosed s) {u : â„• â†’ X} (hu : Tendsto u atTop (ğ“ a))
    (hus : âˆ€ n, u n âˆˆ s) : a âˆˆ s :=
  hs.mem_of_tendsto hu (Eventually.of_forall hus)

example {s : Set X} : a âˆˆ closure s â†” âˆ€ Îµ > 0, âˆƒ b âˆˆ s, a âˆˆ Metric.ball b Îµ :=
  Metric.mem_closure_iff
-- QUOTE.

/- OMIT:
Do the next exercise without using `mem_closure_iff_seq_limit`
OMIT. -/
/- TEXT:
æ¬¡ã®æ¼”ç¿’å•é¡Œã‚’ `mem_closure_iff_seq_limit` ã‚’ä½¿ã‚ãšã«è¨¼æ˜ã—ã¦ã¿ã¾ã—ã‚‡ã†ï¼
BOTH: -/
-- QUOTE:
example {u : â„• â†’ X} (hu : Tendsto u atTop (ğ“ a)) {s : Set X} (hs : âˆ€ n, u n âˆˆ s) :
    a âˆˆ closure s :=
  sorry
-- QUOTE.

-- SOLUTIONS:
example {u : â„• â†’ X} (hu : Tendsto u atTop (ğ“ a)) {s : Set X} (hs : âˆ€ n, u n âˆˆ s) : a âˆˆ closure s := by
  rw [Metric.tendsto_atTop] at hu
  rw [Metric.mem_closure_iff]
  intro Îµ Îµ_pos
  rcases hu Îµ Îµ_pos with âŸ¨N, hNâŸ©
  refine âŸ¨u N, hs _, ?_âŸ©
  rw [dist_comm]
  exact hN N le_rfl

/- OMIT:

Remember from the filters sections that neighborhood filters play a big role in Mathlib.
In the metric space context, the crucial point is that balls provide bases for those filters.
The main lemmas here are ``Metric.nhds_basis_ball`` and ``Metric.nhds_basis_closedBall``
that claim this for open and closed balls with positive radius. The center point is an implicit
argument so we can invoke ``Filter.HasBasis.mem_iff`` as in the following example.

OMIT. -/
/- TEXT:
ãƒ•ã‚£ãƒ«ã‚¿ã«ã¤ã„ã¦ã®ç¯€ã§ï¼ŒMathlibã§ã¯è¿‘å‚ã®ãƒ•ã‚£ãƒ«ã‚¿ãŒå¤§ããªå½¹å‰²ã‚’æœãŸã—ã¦ã„ãŸã“ã¨ã‚’æ€ã„å‡ºã—ã¦ãã ã•ã„ï¼è·é›¢ç©ºé–“ã®æ–‡è„ˆã§ã®é‡è¦ãªãƒã‚¤ãƒ³ãƒˆã¯ï¼Œçƒä½“ãŒã“ã‚Œã‚‰ã®ãƒ•ã‚£ãƒ«ã‚¿ã®åŸºåº•ã¨ãªã‚‹ã“ã¨ã§ã™ï¼ã“ã“ã§ã®ä¸»ãªè£œé¡Œã¯ ``Metric.nhds_basis_ball`` ã¨ ``Metric.nhds_basis_closedBall`` ã§ï¼Œæ­£ã®åŠå¾„ã‚’æŒã¤é–‹çƒã¨é–‰çƒã«å¯¾ã—ã¦ä¸»å¼µã•ã‚Œã‚‹ã‚‚ã®ã§ã™ï¼ä¸­å¿ƒç‚¹ã¯æš—é»™ã®å¼•æ•°ã§ã‚ã‚‹ãŸã‚ï¼Œæ¬¡ã®ä¾‹ã®ã‚ˆã†ã« ``Filter.HasBasis.mem_iff`` ã‚’å‘¼ã³å‡ºã™ã“ã¨ãŒã§ãã¾ã™ï¼
BOTH: -/
-- QUOTE:
example {x : X} {s : Set X} : s âˆˆ ğ“ x â†” âˆƒ Îµ > 0, Metric.ball x Îµ âŠ† s :=
  Metric.nhds_basis_ball.mem_iff

example {x : X} {s : Set X} : s âˆˆ ğ“ x â†” âˆƒ Îµ > 0, Metric.closedBall x Îµ âŠ† s :=
  Metric.nhds_basis_closedBall.mem_iff
-- QUOTE.

/- OMIT:

Compactness
^^^^^^^^^^^

OMIT. -/
/- TEXT:

ã‚³ãƒ³ãƒ‘ã‚¯ãƒˆæ€§
^^^^^^^^^^^^^^

TEXT. -/
/- OMIT:
Compactness is an important topological notion. It distinguishes subsets of a metric space
that enjoy the same kind of properties as segments in reals compared to other intervals:

OMIT. -/
/- TEXT:
ã‚³ãƒ³ãƒ‘ã‚¯ãƒˆæ€§ã¯ä½ç›¸å¹¾ä½•å­¦çš„ã«é‡è¦ãªæ¦‚å¿µã§ã™ï¼ã“ã‚Œã¯ä»–ã®åŒºé–“ã¨æ¯”ã¹ã¦å®Ÿæ•°ã®ç·šåˆ†ã«è¿‘ã„æ€§è³ªã‚’æº€è¶³ã™ã‚‹è·é›¢ç©ºé–“ã®éƒ¨åˆ†é›†åˆã‚’è­˜åˆ¥ã™ã‚‹ã‚‚ã®ã§ã™ï¼š

TEXT. -/
/- OMIT:
* Any sequence taking value in a compact set has a subsequence that converges in this set
* Any continuous function on a nonempty compact set with values in real numbers is bounded and
  achieves its bounds somewhere (this is called the extreme values theorem).
* Compact sets are closed sets.

OMIT. -/
/- TEXT:
* ã‚³ãƒ³ãƒ‘ã‚¯ãƒˆãªé›†åˆã‹ã‚‰å€¤ã‚’å–ã‚‹æ•°åˆ—ã¯ã“ã®é›†åˆå†…ã«åæŸã™ã‚‹éƒ¨åˆ†åˆ—ã‚’æŒã¤
* å®Ÿæ•°å€¤ã‚’æŒã¤ç©ºã§ãªã„ã‚³ãƒ³ãƒ‘ã‚¯ãƒˆãªé›†åˆä¸Šã®é€£ç¶šé–¢æ•°ã¯ã™ã¹ã¦å¢ƒç•Œã‚’æŒã¡ï¼Œã©ã“ã‹ã§ãã®å¢ƒç•Œã«åˆ°é”ã™ã‚‹ï¼ˆã“ã‚Œã‚’æ¥µå€¤ã®å®šç†ã¨å‘¼ã³ã¾ã™ï¼‰
* ã‚³ãƒ³ãƒ‘ã‚¯ãƒˆãªé›†åˆã¯é–‰é›†åˆã§ã‚ã‚‹

TEXT. -/
/- OMIT:
Let us first check that the unit interval in reals is indeed a compact set, and then check the above
claims for compact sets in general metric spaces. In the second statement we only
need continuity on the given set so we will use ``ContinuousOn`` instead of ``Continuous``, and
we will give separate statements for the minimum and the maximum. Of course all these results
are deduced from more general versions, some of which will be discussed in later sections.

OMIT. -/
/- TEXT:
ã¾ãšï¼Œå®Ÿæ•°ã®å˜ä½åŒºé–“ãŒå®Ÿéš›ã«ã‚³ãƒ³ãƒ‘ã‚¯ãƒˆé›†åˆã§ã‚ã‚‹ã“ã¨ã‚’ç¢ºèªã—ï¼Œæ¬¡ã«ä¸€èˆ¬çš„ãªè·é›¢ç©ºé–“ã®ã‚³ãƒ³ãƒ‘ã‚¯ãƒˆé›†åˆã«ã¤ã„ã¦ä¸Šè¨˜ã®ä¸»å¼µã‚’ç¢ºèªã—ã¦ã¿ã¾ã—ã‚‡ã†ï¼2ã¤ç›®ã®æ–‡ã§ã¯ï¼Œä¸ãˆã‚‰ã‚ŒãŸé›†åˆã®é€£ç¶šæ€§ã ã‘ãŒå¿…è¦ã§ã‚ã‚‹ãŸã‚ï¼Œ ``Continuous`` ã®ä»£ã‚ã‚Šã« ``ContinuousOn`` ã‚’ä½¿ã„ï¼Œæœ€å°å€¤ã¨æœ€å¤§å€¤ã«ã¤ã„ã¦åˆ¥ã€…ã®å‘½é¡Œã‚’ä¸ãˆã¾ã™ï¼ã‚‚ã¡ã‚ã‚“ï¼Œã“ã‚Œã‚‰ã®çµæœã¯ã™ã¹ã¦ã‚ˆã‚Šä¸€èˆ¬çš„ãªã‚‚ã®ã‹ã‚‰å°ã‹ã‚Œã¾ã™ï¼ãã®ã†ã¡ã®ã„ãã¤ã‹ã¯ã“ã®å¾Œã®ç¯€ã§è­°è«–ã—ã¾ã™ï¼
BOTH: -/
-- QUOTE:
example : IsCompact (Set.Icc 0 1 : Set â„) :=
  isCompact_Icc

example {s : Set X} (hs : IsCompact s) {u : â„• â†’ X} (hu : âˆ€ n, u n âˆˆ s) :
    âˆƒ a âˆˆ s, âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ Tendsto (u âˆ˜ Ï†) atTop (ğ“ a) :=
  hs.tendsto_subseq hu

example {s : Set X} (hs : IsCompact s) (hs' : s.Nonempty) {f : X â†’ â„}
      (hfs : ContinuousOn f s) :
    âˆƒ x âˆˆ s, âˆ€ y âˆˆ s, f x â‰¤ f y :=
  hs.exists_isMinOn hs' hfs

example {s : Set X} (hs : IsCompact s) (hs' : s.Nonempty) {f : X â†’ â„}
      (hfs : ContinuousOn f s) :
    âˆƒ x âˆˆ s, âˆ€ y âˆˆ s, f y â‰¤ f x :=
  hs.exists_isMaxOn hs' hfs

example {s : Set X} (hs : IsCompact s) : IsClosed s :=
  hs.isClosed
-- QUOTE.

/- OMIT:

We can also specify that a metric spaces is globally compact, using an extra ``Prop``-valued type class:

OMIT. -/
/- TEXT:
ã¾ãŸï¼Œ ``Prop``-å€¤ã®å‹ã‚¯ãƒ©ã‚¹ã‚’è¿½åŠ ã™ã‚‹ã“ã¨ã§ï¼Œè·é›¢ç©ºé–“ãŒå¤§åŸŸçš„ã«ã‚³ãƒ³ãƒ‘ã‚¯ãƒˆã§ã‚ã‚‹ã“ã¨ã‚’æŒ‡å®šã™ã‚‹ã“ã¨ã‚‚ã§ãã¾ã™ï¼š
BOTH: -/
-- QUOTE:
example {X : Type*} [MetricSpace X] [CompactSpace X] : IsCompact (univ : Set X) :=
  isCompact_univ
-- QUOTE.

/- OMIT:

In a compact metric space any closed set is compact, this is ``IsClosed.isCompact``.

OMIT. -/
/- TEXT:
ã‚³ãƒ³ãƒ‘ã‚¯ãƒˆãªè·é›¢ç©ºé–“ã«ãŠã„ã¦ï¼Œã™ã¹ã¦ã®é–‰é›†åˆã¯ã‚³ãƒ³ãƒ‘ã‚¯ãƒˆã§ã™ï¼ã“ã‚ŒãŒ ``IsClosed.isCompact`` ã§ã™ï¼
BOTH: -/
#check IsCompact.isClosed

/- OMIT:
Uniformly continuous functions
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

OMIT. -/
/- TEXT:

ä¸€æ§˜é€£ç¶šé–¢æ•°
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

TEXT. -/
/- OMIT:
We now turn to uniformity notions on metric spaces : uniformly continuous functions, Cauchy sequences and completeness.
Again those are defined in a more general context but we have lemmas in the metric name space to access their elementary definitions.
We start with uniform continuity.

OMIT. -/
/- TEXT:
ã“ã“ã§è·é›¢ç©ºé–“ã®ä¸€æ§˜æ€§ã«ç«‹ã¡å…¥ã‚Šã¾ã—ã‚‡ã†ï¼šä¾‹ãˆã°ä¸€æ§˜é€£ç¶šé–¢æ•°ï¼Œã‚³ãƒ¼ã‚·ãƒ¼åˆ—ï¼Œå®Œå…¨æ€§ãªã©ã§ã™ï¼ã“ã‚Œã‚‰ã‚‚ã¾ãŸã‚ˆã‚Šä¸€èˆ¬çš„ãªæ–‡è„ˆã§å®šç¾©ã•ã‚Œã¾ã™ãŒï¼Œãã‚Œã‚‰ã®åˆæ­©çš„ãªå®šç¾©ã«è§¦ã‚Œã‚‹ãŸã‚ã«è·é›¢ã®åå‰ç©ºé–“ã§ã®è£œé¡Œã‚’ä½œã‚Šã¾ã—ã‚‡ã†ï¼ã¾ãšä¸€æ§˜é€£ç¶šæ€§ã‹ã‚‰å§‹ã‚ã¾ã—ã‚‡ã†ï¼
BOTH: -/
-- QUOTE:
example {X : Type*} [MetricSpace X] {Y : Type*} [MetricSpace Y] {f : X â†’ Y} :
    UniformContinuous f â†”
      âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ {a b : X}, dist a b < Î´ â†’ dist (f a) (f b) < Îµ :=
  Metric.uniformContinuous_iff
-- QUOTE.

/- OMIT:
In order to practice manipulating all those definitions, we will prove that continuous
functions from a compact metric space to a metric space are uniformly continuous
(we will see a more general version in a later section).

OMIT. -/
/- TEXT:
ã“ã‚Œã‚‰ã®å®šç¾©ã‚’ã™ã¹ã¦æ‰±ã†ç·´ç¿’ã¨ã—ã¦ï¼Œã‚³ãƒ³ãƒ‘ã‚¯ãƒˆãªè·é›¢ç©ºé–“ã‹ã‚‰è·é›¢ç©ºé–“ã¸ã®é€£ç¶šé–¢æ•°ãŒä¸€æ§˜ã«é€£ç¶šã§ã‚ã‚‹ã“ã¨ã‚’è¨¼æ˜ã—ã¾ã™ï¼ï¼ˆå¾Œã®ç¯€ã§ã‚ˆã‚Šä¸€èˆ¬çš„ãªãƒãƒ¼ã‚¸ãƒ§ãƒ³ã‚’è¦‹ã¾ã™ï¼‰

TEXT. -/
/- OMIT:
We will first give an informal sketch. Let ``f : X â†’ Y`` be a continuous function from
a compact metric space to a metric space.
We fix ``Îµ > 0`` and start looking for some ``Î´``.

OMIT. -/
/- TEXT:
ã¾ãšéå½¢å¼çš„ãªè¨¼æ˜ã®ã‚ã‚‰ã™ã˜ã‚’èª¬æ˜ã—ã¾ã—ã‚‡ã†ï¼ ``f : X â†’ Y`` ã‚’ã‚³ãƒ³ãƒ‘ã‚¯ãƒˆãªè·é›¢ç©ºé–“ã‹ã‚‰è·é›¢ç©ºé–“ã¸ã®é€£ç¶šé–¢æ•°ã¨ã—ã¾ã™ï¼ã“ã“ã§ï¼Œ ``Îµ > 0`` ã‚’å›ºå®šã—ï¼Œã‚ã‚‹ ``Î´`` ã‚’æ¢ã—å§‹ã‚ã¾ã™ï¼

TEXT. -/
/- OMIT:
Let ``Ï† : X Ã— X â†’ â„ := fun p â†¦ dist (f p.1) (f p.2)`` and let ``K := { p : X Ã— X | Îµ â‰¤ Ï† p }``.
Observe ``Ï†`` is continuous since ``f`` and distance are continuous.
And ``K`` is clearly closed (use ``isClosed_le``) hence compact since ``X`` is compact.

OMIT. -/
/- TEXT:
``Ï† : X Ã— X â†’ â„ := fun p â†¦ dist (f p.1) (f p.2)`` ã¨ ``K := { p : X Ã— X | Îµ â‰¤ Ï† p }`` ã‚’ä»®å®šã—ã¾ã™ï¼ ``f`` ã¨è·é›¢ã¯é€£ç¶šã§ã‚ã‚‹ãŸã‚ï¼Œ ``Ï†`` ã¯é€£ç¶šã§ã™ï¼ã¾ãŸï¼Œ ``K`` ã¯æ˜ã‚‰ã‹ã«é–‰ã˜ã¦ã„ã‚‹ï¼ˆ ``isClosed_le`` ã‚’ä½¿ã„ã¾ã™ï¼‰ã®ã§ï¼Œ ``X`` ã¯ã‚³ãƒ³ãƒ‘ã‚¯ãƒˆã§ã‚ã‚‹ã“ã¨ã‹ã‚‰ ``K`` ã¯ã‚³ãƒ³ãƒ‘ã‚¯ãƒˆã«ãªã‚Šã¾ã™ï¼

TEXT. -/
/- OMIT:
Then we discuss two possibilities using ``eq_empty_or_nonempty``.
If ``K`` is empty then we are clearly done (we can set ``Î´ = 1`` for instance).
So let's assume ``K`` is not empty, and use the extreme value theorem to choose ``(xâ‚€, xâ‚)`` attaining the infimum
of the distance function on ``K``. We can then set ``Î´ = dist xâ‚€ xâ‚`` and check everything works.

OMIT. -/
/- TEXT:
ã“ã“ã§ ``eq_empty_or_nonempty`` ã‚’ä½¿ã£ã¦2ã¤ã®å¯èƒ½æ€§ã‚’è­°è«–ã—ã¾ã™ï¼ã‚‚ã—ï¼Œ ``K`` ãŒç©ºã§ã‚ã‚Œã°ï¼Œæ˜ã‚‰ã‹ã«æˆã‚Šç«‹ã¡ã¾ã™ï¼ˆä¾‹ãˆã° ``Î´ = 1`` ã¨è¨­å®šã§ãã¾ã™ï¼‰ï¼ãã“ã§ï¼Œ ``K`` ãŒç©ºã§ãªã„ã¨ä»®å®šã—ï¼Œæ¥µå€¤ã®å®šç†ã‚’ä½¿ã£ã¦ ``(xâ‚€, xâ‚)`` ã‚’é¸æŠã—ï¼Œ ``K`` ä¸Šã®è·é›¢é–¢æ•°ã®ä¸‹é™ã‚’æ±‚ã‚ã¾ã™ï¼ãã—ã¦ ``Î´ = dist xâ‚€ xâ‚`` ã¨è¨­å®šã—ï¼Œã“ã‚ŒãŒã†ã¾ãã„ãã“ã¨ã‚’ç¢ºèªã—ã¾ã™ï¼
BOTH: -/
-- QUOTE:
example {X : Type*} [MetricSpace X] [CompactSpace X]
      {Y : Type*} [MetricSpace Y] {f : X â†’ Y}
    (hf : Continuous f) : UniformContinuous f :=
  sorry
-- QUOTE.

-- SOLUTIONS:
example {X : Type*} [MetricSpace X] [CompactSpace X] {Y : Type*} [MetricSpace Y] {f : X â†’ Y}
    (hf : Continuous f) : UniformContinuous f := by
  rw [Metric.uniformContinuous_iff]
  intro Îµ Îµ_pos
  let Ï† : X Ã— X â†’ â„ := fun p â†¦ dist (f p.1) (f p.2)
  have Ï†_cont : Continuous Ï† := hf.fst'.dist hf.snd'
  let K := { p : X Ã— X | Îµ â‰¤ Ï† p }
  have K_closed : IsClosed K := isClosed_le continuous_const Ï†_cont
  have K_cpct : IsCompact K := K_closed.isCompact
  rcases eq_empty_or_nonempty K with hK | hK
  Â· use 1, by norm_num
    intro x y _
    have : (x, y) âˆ‰ K := by simp [hK]
    simpa [K] using this
  Â· rcases K_cpct.exists_isMinOn hK continuous_dist.continuousOn with âŸ¨âŸ¨xâ‚€, xâ‚âŸ©, xx_in, HâŸ©
    use dist xâ‚€ xâ‚
    constructor
    Â· change _ < _
      rw [dist_pos]
      intro h
      have : Îµ â‰¤ 0 := by simpa [K, Ï†, *] using xx_in
      linarith
    Â· intro x x'
      contrapose!
      intro (hxx' : (x, x') âˆˆ K)
      exact H hxx'

/- OMIT:
Completeness
^^^^^^^^^^^^

OMIT. -/
/- TEXT:
å®Œå…¨æ€§
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

TEXT. -/
/- OMIT:
A Cauchy sequence in a metric space is a sequence whose terms get closer and closer to each other.
There are a couple of equivalent ways to state that idea.
In particular converging sequences are Cauchy. The converse is true only in so-called *complete*
spaces.


OMIT. -/
/- TEXT:
è·é›¢ç©ºé–“ã«ãŠã‘ã‚‹ã‚³ãƒ¼ã‚·ãƒ¼åˆ—ã¨ã¯ï¼Œå„é …ãŒã©ã‚“ã©ã‚“äº’ã„ã«è¿‘ã¥ã„ã¦ã„ãæ•°åˆ—ã®ã“ã¨ã§ã™ï¼ã“ã®è€ƒãˆæ–¹ã«ã¯ã„ãã¤ã‹ã®ç­‰ä¾¡ãªè¨€ã„æ–¹ãŒã‚ã‚Šã¾ã™ï¼ç‰¹ã«åæŸã™ã‚‹æ•°åˆ—ã¯ã‚³ãƒ¼ã‚·ãƒ¼åˆ—ã§ã™ï¼ãã®é€†ã¯ã„ã‚ã‚†ã‚‹ **å®Œå…¨** ï¼ˆcompleteï¼‰ç©ºé–“ã«ãŠã„ã¦ã®ã¿æˆã‚Šç«‹ã¡ã¾ã™ï¼
BOTH: -/
-- QUOTE:
example (u : â„• â†’ X) :
    CauchySeq u â†” âˆ€ Îµ > 0, âˆƒ N : â„•, âˆ€ m â‰¥ N, âˆ€ n â‰¥ N, dist (u m) (u n) < Îµ :=
  Metric.cauchySeq_iff

example (u : â„• â†’ X) :
    CauchySeq u â†” âˆ€ Îµ > 0, âˆƒ N : â„•, âˆ€ n â‰¥ N, dist (u n) (u N) < Îµ :=
  Metric.cauchySeq_iff'

example [CompleteSpace X] (u : â„• â†’ X) (hu : CauchySeq u) :
    âˆƒ x, Tendsto u atTop (ğ“ x) :=
  cauchySeq_tendsto_of_complete hu
-- QUOTE.

/- OMIT:

We'll practice using this definition by proving a convenient criterion which is a special case of a
criterion appearing in Mathlib. This is also a good opportunity to practice using big sums in
a geometric context. In addition to the explanations from the filters section, you will probably need
``tendsto_pow_atTop_nhds_zero_of_lt_one``, ``Tendsto.mul`` and ``dist_le_range_sum_dist``.
OMIT. -/
/- TEXT:
ã“ã®å®šç¾©ã‚’ä½¿ã†ç·´ç¿’ã¨ã—ã¦ä»¥ä¸‹ã®ä¾¿åˆ©ãªæ¡ä»¶ã‚’è¨¼æ˜ã—ã¾ã—ã‚‡ã†ï¼ã“ã‚Œã¯Mathlibã«ç™»å ´ã™ã‚‹æ¡ä»¶ã®ç‰¹æ®Šãªã‚±ãƒ¼ã‚¹ã§ã™ï¼ã¾ãŸï¼Œã“ã‚Œã¯å¹¾ä½•å­¦çš„ãªæ–‡è„ˆã§å¤§ããªå’Œã‚’ä½¿ã†ç·´ç¿’ã‚’ã™ã‚‹è‰¯ã„æ©Ÿä¼šã§ã‚‚ã‚ã‚Šã¾ã™ï¼ãƒ•ã‚£ãƒ«ã‚¿ã®ã‚»ã‚¯ã‚·ãƒ§ãƒ³ã®èª¬æ˜ã«åŠ ãˆã¦ï¼ŒãŠãã‚‰ã ``tendsto_pow_atTop_nhds_zero_of_lt_one`` ã¨ ``Tendsto.mul`` ï¼Œ ``dist_le_range_sum_dist`` ãŒå¿…è¦ã«ãªã‚‹ã§ã—ã‚‡ã†ï¼
BOTH: -/
open BigOperators

open Finset

-- QUOTE:
theorem cauchySeq_of_le_geometric_two' {u : â„• â†’ X}
    (hu : âˆ€ n : â„•, dist (u n) (u (n + 1)) â‰¤ (1 / 2) ^ n) : CauchySeq u := by
  rw [Metric.cauchySeq_iff']
  intro Îµ Îµ_pos
  obtain âŸ¨N, hNâŸ© : âˆƒ N : â„•, 1 / 2 ^ N * 2 < Îµ := by sorry
  use N
  intro n hn
  obtain âŸ¨k, rfl : n = N + kâŸ© := le_iff_exists_add.mp hn
  calc
    dist (u (N + k)) (u N) = dist (u (N + 0)) (u (N + k)) := sorry
    _ â‰¤ âˆ‘ i in range k, dist (u (N + i)) (u (N + (i + 1))) := sorry
    _ â‰¤ âˆ‘ i in range k, (1 / 2 : â„) ^ (N + i) := sorry
    _ = 1 / 2 ^ N * âˆ‘ i in range k, (1 / 2 : â„) ^ i := sorry
    _ â‰¤ 1 / 2 ^ N * 2 := sorry
    _ < Îµ := sorry

-- QUOTE.

-- SOLUTIONS:
example {u : â„• â†’ X} (hu : âˆ€ n : â„•, dist (u n) (u (n + 1)) â‰¤ (1 / 2) ^ n) : CauchySeq u := by
  rw [Metric.cauchySeq_iff']
  intro Îµ Îµ_pos
  obtain âŸ¨N, hNâŸ© : âˆƒ N : â„•, 1 / 2 ^ N * 2 < Îµ := by
    have : Tendsto (fun N : â„• â†¦ (1 / 2 ^ N * 2 : â„)) atTop (ğ“ 0) := by
      rw [â† zero_mul (2 : â„)]
      apply Tendsto.mul
      simp_rw [â† one_div_pow (2 : â„)]
      apply tendsto_pow_atTop_nhds_zero_of_lt_one <;> linarith
      exact tendsto_const_nhds
    rcases(atTop_basis.tendsto_iff (nhds_basis_Ioo_pos (0 : â„))).mp this Îµ Îµ_pos with âŸ¨N, _, hNâŸ©
    exact âŸ¨N, by simpa using (hN N left_mem_Ici).2âŸ©
  use N
  intro n hn
  obtain âŸ¨k, rfl : n = N + kâŸ© := le_iff_exists_add.mp hn
  calc
    dist (u (N + k)) (u N) = dist (u (N + 0)) (u (N + k)) := by rw [dist_comm, add_zero]
    _ â‰¤ âˆ‘ i in range k, dist (u (N + i)) (u (N + (i + 1))) :=
      (dist_le_range_sum_dist (fun i â†¦ u (N + i)) k)
    _ â‰¤ âˆ‘ i in range k, (1 / 2 : â„) ^ (N + i) := (sum_le_sum fun i _ â†¦ hu <| N + i)
    _ = 1 / 2 ^ N * âˆ‘ i in range k, (1 / 2 : â„) ^ i := by simp_rw [â† one_div_pow, pow_add, â† mul_sum]
    _ â‰¤ 1 / 2 ^ N * 2 :=
      (mul_le_mul_of_nonneg_left (sum_geometric_two_le _)
        (one_div_nonneg.mpr (pow_nonneg (zero_le_two : (0 : â„) â‰¤ 2) _)))
    _ < Îµ := hN


/- OMIT:

We are ready for the final boss of this section: Baire's theorem for complete metric spaces!
The proof skeleton below shows interesting techniques. It uses the ``choose`` tactic in its exclamation
mark variant (you should experiment with removing this exclamation mark) and it shows how to
define something inductively in the middle of a proof using ``Nat.rec_on``.

OMIT. -/
/- TEXT:
æœ¬ç¯€ã®ãƒ©ã‚¹ãƒœã‚¹ã¸æŒ‘ã‚€æº–å‚™ãŒæ•´ã„ã¾ã—ãŸï¼šå®Œå…¨è·é›¢ç©ºé–“ã«å¯¾ã™ã‚‹ãƒ™ãƒ¼ãƒ«ã®å®šç†ã§ã™ï¼ä»¥ä¸‹ã®è¨¼æ˜ã®éª¨çµ„ã¿ã§ã¯èˆˆå‘³æ·±ã„ãƒ†ã‚¯ãƒ‹ãƒƒã‚¯ã‚’é§†ä½¿ã—ã¦ã„ã¾ã™ï¼ã“ã‚Œã¯ ``choose`` ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ã‚’ä½¿ã£ã¦ãŠã‚Šï¼ˆã“ã®ãƒ“ãƒƒã‚¯ãƒªãƒãƒ¼ã‚¯ã‚’å‰Šé™¤ã—ã¦ã¿ã‚‹ã“ã¨ã‚’ãŠã™ã™ã‚ã—ã¾ã™ï¼‰ï¼Œ ``Nat.rec_on`` ã‚’ä½¿ã£ã¦è¨¼æ˜ã®é€”ä¸­ã§å¸°ç´çš„ã«ä½•ã‹ã‚’å®šç¾©ã™ã‚‹æ–¹æ³•ã‚’ç¤ºã—ã¦ã„ã¾ã™ï¼
BOTH: -/
-- QUOTE:
open Metric

example [CompleteSpace X] (f : â„• â†’ Set X) (ho : âˆ€ n, IsOpen (f n)) (hd : âˆ€ n, Dense (f n)) :
    Dense (â‹‚ n, f n) := by
  let B : â„• â†’ â„ := fun n â†¦ (1 / 2) ^ n
  have Bpos : âˆ€ n, 0 < B n
  sorry
  /- Translate the density assumption into two functions `center` and `radius` associating
    to any n, x, Î´, Î´pos a center and a positive radius such that
    `closedBall center radius` is included both in `f n` and in `closedBall x Î´`.
    We can also require `radius â‰¤ (1/2)^(n+1)`, to ensure we get a Cauchy sequence later. -/
  have :
    âˆ€ (n : â„•) (x : X),
      âˆ€ Î´ > 0, âˆƒ y : X, âˆƒ r > 0, r â‰¤ B (n + 1) âˆ§ closedBall y r âŠ† closedBall x Î´ âˆ© f n :=
    by sorry
  choose! center radius Hpos HB Hball using this
  intro x
  rw [mem_closure_iff_nhds_basis nhds_basis_closedBall]
  intro Îµ Îµpos
  /- `Îµ` is positive. We have to find a point in the ball of radius `Îµ` around `x`
    belonging to all `f n`. For this, we construct inductively a sequence
    `F n = (c n, r n)` such that the closed ball `closedBall (c n) (r n)` is included
    in the previous ball and in `f n`, and such that `r n` is small enough to ensure
    that `c n` is a Cauchy sequence. Then `c n` converges to a limit which belongs
    to all the `f n`. -/
  let F : â„• â†’ X Ã— â„ := fun n â†¦
    Nat.recOn n (Prod.mk x (min Îµ (B 0)))
      fun n p â†¦ Prod.mk (center n p.1 p.2) (radius n p.1 p.2)
  let c : â„• â†’ X := fun n â†¦ (F n).1
  let r : â„• â†’ â„ := fun n â†¦ (F n).2
  have rpos : âˆ€ n, 0 < r n := by sorry
  have rB : âˆ€ n, r n â‰¤ B n := by sorry
  have incl : âˆ€ n, closedBall (c (n + 1)) (r (n + 1)) âŠ† closedBall (c n) (r n) âˆ© f n := by
    sorry
  have cdist : âˆ€ n, dist (c n) (c (n + 1)) â‰¤ B n := by sorry
  have : CauchySeq c := cauchySeq_of_le_geometric_two' cdist
  -- as the sequence `c n` is Cauchy in a complete space, it converges to a limit `y`.
  rcases cauchySeq_tendsto_of_complete this with âŸ¨y, ylimâŸ©
  -- this point `y` will be the desired point. We will check that it belongs to all
  -- `f n` and to `ball x Îµ`.
  use y
  have I : âˆ€ n, âˆ€ m â‰¥ n, closedBall (c m) (r m) âŠ† closedBall (c n) (r n) := by sorry
  have yball : âˆ€ n, y âˆˆ closedBall (c n) (r n) := by sorry
  sorry
-- QUOTE.

-- SOLUTIONS:
example [CompleteSpace X] (f : â„• â†’ Set X) (ho : âˆ€ n, IsOpen (f n)) (hd : âˆ€ n, Dense (f n)) :
    Dense (â‹‚ n, f n) := by
  let B : â„• â†’ â„ := fun n â†¦ (1 / 2) ^ n
  have Bpos : âˆ€ n, 0 < B n := fun n â†¦ pow_pos sorry n
  /- Translate the density assumption into two functions `center` and `radius` associating
    to any n, x, Î´, Î´pos a center and a positive radius such that
    `closedBall center radius` is included both in `f n` and in `closedBall x Î´`.
    We can also require `radius â‰¤ (1/2)^(n+1)`, to ensure we get a Cauchy sequence later. -/
  have :
    âˆ€ (n : â„•) (x : X),
      âˆ€ Î´ > 0, âˆƒ y : X, âˆƒ r > 0, r â‰¤ B (n + 1) âˆ§ closedBall y r âŠ† closedBall x Î´ âˆ© f n := by
    intro n x Î´ Î´pos
    have : x âˆˆ closure (f n) := hd n x
    rcases Metric.mem_closure_iff.1 this (Î´ / 2) (half_pos Î´pos) with âŸ¨y, ys, xyâŸ©
    rw [dist_comm] at xy
    obtain âŸ¨r, rpos, hrâŸ© : âˆƒ r > 0, closedBall y r âŠ† f n :=
      nhds_basis_closedBall.mem_iff.1 (isOpen_iff_mem_nhds.1 (ho n) y ys)
    refine âŸ¨y, min (min (Î´ / 2) r) (B (n + 1)), ?_, ?_, fun z hz â†¦ âŸ¨?_, ?_âŸ©âŸ©
    show 0 < min (min (Î´ / 2) r) (B (n + 1))
    exact lt_min (lt_min (half_pos Î´pos) rpos) (Bpos (n + 1))
    show min (min (Î´ / 2) r) (B (n + 1)) â‰¤ B (n + 1)
    exact min_le_right _ _
    show z âˆˆ closedBall x Î´
    exact
      calc
        dist z x â‰¤ dist z y + dist y x := dist_triangle _ _ _
        _ â‰¤ min (min (Î´ / 2) r) (B (n + 1)) + Î´ / 2 := (add_le_add hz xy.le)
        _ â‰¤ Î´ / 2 + Î´ / 2 := (add_le_add_right ((min_le_left _ _).trans (min_le_left _ _)) _)
        _ = Î´ := add_halves Î´

    show z âˆˆ f n
    exact
      hr
        (calc
          dist z y â‰¤ min (min (Î´ / 2) r) (B (n + 1)) := hz
          _ â‰¤ r := (min_le_left _ _).trans (min_le_right _ _)
          )
  choose! center radius Hpos HB Hball using this
  refine fun x â†¦ (mem_closure_iff_nhds_basis nhds_basis_closedBall).2 fun Îµ Îµpos â†¦ ?_
  /- `Îµ` is positive. We have to find a point in the ball of radius `Îµ` around `x` belonging to all
    `f n`. For this, we construct inductively a sequence `F n = (c n, r n)` such that the closed ball
    `closedBall (c n) (r n)` is included in the previous ball and in `f n`, and such that
    `r n` is small enough to ensure that `c n` is a Cauchy sequence. Then `c n` converges to a
    limit which belongs to all the `f n`. -/
  let F : â„• â†’ X Ã— â„ := fun n â†¦
    Nat.recOn n (Prod.mk x (min Îµ (B 0))) fun n p â†¦ Prod.mk (center n p.1 p.2) (radius n p.1 p.2)
  let c : â„• â†’ X := fun n â†¦ (F n).1
  let r : â„• â†’ â„ := fun n â†¦ (F n).2
  have rpos : âˆ€ n, 0 < r n := by
    intro n
    induction' n with n hn
    exact lt_min Îµpos (Bpos 0)
    exact Hpos n (c n) (r n) hn
  have rB : âˆ€ n, r n â‰¤ B n := by
    intro n
    induction' n with n hn
    exact min_le_right _ _
    exact HB n (c n) (r n) (rpos n)
  have incl : âˆ€ n, closedBall (c (n + 1)) (r (n + 1)) âŠ† closedBall (c n) (r n) âˆ© f n := fun n â†¦
    Hball n (c n) (r n) (rpos n)
  have cdist : âˆ€ n, dist (c n) (c (n + 1)) â‰¤ B n := by
    intro n
    rw [dist_comm]
    have A : c (n + 1) âˆˆ closedBall (c (n + 1)) (r (n + 1)) :=
      mem_closedBall_self (rpos <| n + 1).le
    have I :=
      calc
        closedBall (c (n + 1)) (r (n + 1)) âŠ† closedBall (c n) (r n) :=
          (incl n).trans Set.inter_subset_left
        _ âŠ† closedBall (c n) (B n) := closedBall_subset_closedBall (rB n)

    exact I A
  have : CauchySeq c := cauchySeq_of_le_geometric_two' cdist
  -- as the sequence `c n` is Cauchy in a complete space, it converges to a limit `y`.
  rcases cauchySeq_tendsto_of_complete this with âŸ¨y, ylimâŸ©
  -- this point `y` will be the desired point. We will check that it belongs to all
  -- `f n` and to `ball x Îµ`.
  use y
  have I : âˆ€ n, âˆ€ m â‰¥ n, closedBall (c m) (r m) âŠ† closedBall (c n) (r n) := by
    intro n
    refine Nat.le_induction ?_ fun m hnm h â†¦ ?_
    Â· exact Subset.rfl
    Â· exact (incl m).trans (Set.inter_subset_left.trans h)
  have yball : âˆ€ n, y âˆˆ closedBall (c n) (r n) := by
    intro n
    refine isClosed_ball.mem_of_tendsto ylim ?_
    refine (Filter.eventually_ge_atTop n).mono fun m hm â†¦ ?_
    exact I n m hm (mem_closedBall_self (rpos _).le)
  constructor
  Â· suffices âˆ€ n, y âˆˆ f n by rwa [Set.mem_iInter]
    intro n
    have : closedBall (c (n + 1)) (r (n + 1)) âŠ† f n :=
      Subset.trans (incl n) Set.inter_subset_right
    exact this (yball (n + 1))
  calc
    dist y x â‰¤ r 0 := yball 0
    _ â‰¤ Îµ := min_le_left _ _


/- OMIT:

BOTH: -/
